AC_INIT([znc-js], [1.0])

AC_MSG_NOTICE([*** Configuring JavaScript module for ZNC ***])

AC_CONFIG_SRCDIR([src/znc_js_mod.cpp])
AC_LANG([C++])

CXXFLAGS="$CXXFLAGS "
AC_PROG_CXX
AC_CANONICAL_HOST
AC_SYS_LARGEFILE

PKG_PROG_PKG_CONFIG()


AC_MSG_NOTICE([*** Looking for ZNC... ***])

PKG_CHECK_MODULES([ZNC], [znc])

AC_MSG_CHECKING([ZNC prefix])

ZNC_PREFIX=`pkg-config --variable=prefix znc`

if test "x$ZNC_PREFIX" = "x"
then
	AC_MSG_ERROR([unsuccessful])
else
	AC_MSG_RESULT([$ZNC_PREFIX])
fi

MODLINK=`pkg-config --variable=modlink znc`
MODDIR=`pkg-config --variable=moddir znc`
ZNC_INC=`pkg-config --variable=INC_PATH znc`

if test "$LIBS" != "";
then
	LIBS="$LIBS $ZNC_LIBS"
else
	LIBS=$ZNC_LIBS
fi

CXXFLAGS="$CXXFLAGS $ZNC_CFLAGS $ZNC_INC"


AC_MSG_NOTICE([*** Looking for SpiderMonkey... ***])

AC_SEARCH_LIBS([JS_NewContext], [js])

AC_CHECK_HEADER([jsapi.h], [], [
	AC_CHECK_HEADER([js/jsapi.h], [CXXFLAGS="$CXXFLAGS -DJSAPI_SUBDIR"], [], [#define XP_UNIX])
], [#define XP_UNIX])

AC_MSG_CHECKING([whether SpiderMonkey headers are ok])
AC_COMPILE_IFELSE([
	AC_LANG_PROGRAM([[
		#define XP_UNIX
		#include "js/jsapi.h"
		static JSClass global_class = { "global", JSCLASS_GLOBAL_FLAGS, JS_PropertyStub, JS_PropertyStub,
			JS_PropertyStub, JS_PropertyStub, JS_EnumerateStub, JS_ResolveStub, JS_ConvertStub,
			JS_FinalizeStub, JSCLASS_NO_OPTIONAL_MEMBERS };
		void reportError(JSContext *cx, const char *message, JSErrorReport *report) { }
	]], [[
		JSRuntime *rt = JS_NewRuntime(8L * 1024L * 1024L);
		JSContext *cx = JS_NewContext(rt, 8192);
		JS_SetOptions(cx, JSOPTION_VAROBJFIX);
		JS_SetVersion(cx, JSVERSION_DEFAULT);
		JS_SetErrorReporter(cx, reportError);
		JSObject  *global = JS_NewObject(cx, &global_class, NULL, NULL);
		JS_InitStandardClasses(cx, global);
		JS_DestroyContext(cx);
		JS_DestroyRuntime(rt);
		JS_ShutDown();
	]])], [
		AC_MSG_RESULT([yes])
	], [
		AC_MSG_RESULT([no])
		AC_MSG_ERROR([Basic SpiderMonkey test program failed to compile. Your SpiderMonkey installation seems not ok.])
	])

AC_SUBST([CXXFLAGS])
AC_SUBST([CPPFLAGS])
AC_SUBST([LIBS])
AC_SUBST([MODLINK])
AC_CONFIG_FILES([Makefile])
AC_OUTPUT

echo
echo znc-js AC_PACKAGE_VERSION configured.

