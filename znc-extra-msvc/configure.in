dnl Keep the version number in sync with znc when releasing!
AC_INIT([znc-extra], [0.068])
AC_CONFIG_SRCDIR([antiidle.cpp])
AC_LANG([C++])

# If they called us with some --prefix, add that bindir to our search path
# (eval because $bindir normally expands to ${exec_prefix}/bin)
eval PATH="\${PATH}:${bindir}"

AC_ARG_WITH( [znc-config],
	AS_HELP_STRING([--with-znc-config], [path to znc-config]),
	[ZNC_CONFIG=$withval],
	[AC_PATH_PROG([ZNC_CONFIG], [znc-config], [])])

if test "x$ZNC_CONFIG" = x
then
	AC_MSG_ERROR([Could not find znc-config.
Please rerun with --with-znc-config=/path/to/znc-config])
fi

config_flag()
{
	FLAG="$1"
	VAR="$2"
	MSG="$3"

	AC_MSG_CHECKING([$MSG])
	VALUE=$(${ZNC_CONFIG} ${FLAG})
	AC_MSG_RESULT([$VALUE])
	eval $VAR="\$VALUE"
}

config_flag --version ZNC_VERSION "for ZNC version"

if test "x${ZNC_VERSION}" = x
then
	AC_MSG_ERROR([Could not find out znc version!])
fi

# Now get the rest of the flags
config_flag --cflags CXXFLAGS "for ZNC CXXFLAGS"
config_flag --libs LDFLAGS "for ZNC libs"
config_flag --moddir MODDIR "for ZNC module directory"
config_flag --datadir DATADIR "for ZNC module data directory"

if test "x$DATADIR" = x
then
	AC_MSG_ERROR([Your ZNC is too old. We require at least znc 0.054])
fi

# Added in 0.061
config_flag --modlink MODLINK "for ZNC module link flag"

if test "x$MODLINK" = x
then
	MODLINK=-shared
fi

if test "x$CXX" = x
then
	# Added in 0.061
	config_flag --cxx CXX "for ZNC module compiler"
fi

# AC_PROG_CXX sets CXXFLAGS to "-O2 -g" if it is unset which we don't want
CXXFLAGS="$CXXFLAGS "
AC_PROG_CXX

# Check if we want modtcl
AC_ARG_ENABLE( [tcl],
	AS_HELP_STRING([--enable-tcl], [enable modtcl]),
	[TCL_FLAGS="yes"],
	[TCL_FLAGS="no"])

AC_ARG_WITH( [tcl-flags],
	AS_HELP_STRING([--with-tcl-flags=FLAGS],
		[The flags needed for compiling and linking modtcl]),
	[TCL_FLAGS="$withval"],)

if test x"$TCL_FLAGS" = "xyes"
then
	AC_ARG_WITH( [tcl],
		AS_HELP_STRING([--with-tcl],
			[directory containing tclConfig.sh]),
			TCL_DIR="${withval}")

	# This need most likely most be extended in the future, but I dont think
	# it's a good idea to stuff a shitload of random stuff in here right now
	for path in $TCL_DIR /usr/lib /usr/lib/tcl8.4 /usr/lib/tcl8.5
	do
		file="${path}/tclConfig.sh"
		AC_CHECK_FILE(${file}, [TCL_CONF="$file" ; break])
	done

	if test x"${TCL_CONF}" = x
	then
		# They --enable-tcl'd, so give them some sane default
		TCL_FLAGS="-I/usr/include/tcl -ltcl"
		AC_MSG_WARN([Could not find tclConfig.sh, using some sane defaults.])
	else
		AC_MSG_CHECKING([modtcl flags])
		. ${TCL_CONF}
		# eval because those vars depend on other vars in there
		eval "TCL_LIB_SPEC=\"${TCL_LIB_SPEC}\""
		eval "TCL_INCLUDE_SPEC=\"${TCL_INCLUDE_SPEC}\""
		TCL_FLAGS="$TCL_INCLUDE_SPEC $TCL_LIB_SPEC"
		AC_MSG_RESULT([$TCL_FLAGS])
	fi
fi
if test x"$TCL_FLAGS" = xno
then
	TCL_FLAGS=""
fi

AC_SUBST([CXXFLAGS])
AC_SUBST([LDFLAGS])
AC_SUBST([MODDIR])
AC_SUBST([DATADIR])
AC_SUBST([MODLINK])
AC_SUBST([CXX])
AC_SUBST([TCL_FLAGS])
AC_CONFIG_FILES([Makefile])

AC_OUTPUT

echo
echo znc-extra AC_PACKAGE_VERSION configured
